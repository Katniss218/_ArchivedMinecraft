
options:
	nopermission_message: "&cI'm sorry, but you do not have permission to perform this command. Please contact the server administrators if you believe that this is an error."

on tab complete for "/rpgitem":
	set tab completions for position 1 to "get", and "sync"
	if tab arg-1 = "get":
		loop {rpg::itemdef::*}:
			add loop-index to tab completions for position 2
	if tab arg-1 = "sync":
		set tab completions for position 2 to "hand", "inventory", and "players"

command /rpgitem [<text>] [<text>] [<text>] [<text>] [<text>] [<text>] [<text>]:
	trigger:
		if arg-1 is "get":
			if player has permission "rpg.rpgitem.get":
				if arg-2 is set:
					set {_item_id} to arg-2
					if arg-3 is set:
						if arg-4 is set: # /rpgitem get <prefix> <item> <count>
							set {_item_prefix} to arg-2
							set {_item_count} to arg-4 parsed as integer
						else:
							delete {_item_prefix}
							set {_item_count} to arg-3 parsed as integer
					else:
						set {_item_count} to 1
					give create_rpgitem_stack({_item_id}, {_item_prefix}, {_item_count}, 0) to player
			else:
				send {@nopermission_message} to player
			stop
		if arg-1 is "sync":
			if player has permission "rpg.rpgitem.sync":
				if arg 2 = "hand":
					set player's tool to sync_rpgitem(player's tool, 0)
				if arg 2 = "inventory":
					loop all items in player's inventory:
						set loop-item to sync_rpgitem(loop-item, 0)
				if arg 2 = "players":
					loop all players:
						loop all items in loop-player's inventory:
							set loop-item to sync_rpgitem(loop-item, 0)
			else:
				send {@nopermission_message} to player
			stop
					