
options:
	nopermission_message: "&cI'm sorry, but you don not have permission to perform this command. Please contact the server administrators if you believe that this is an error."

on tab complete for "/xp":
	set tab completions for position 1 to "", "reset", "add", and "remove"

command /xp [<text>] [<text>] [<text>]:
	trigger:
		if arg-1 is not set:
			if {rpgdb::xp::%player's uuid%} is not set:
				set {rpgdb::xp::%player's uuid%} to 0
			if {rpgdb::level::%player's uuid%} is not set:
				set {rpgdb::level::%player's uuid%} to 1
			if player has permission "rpg.xp.view.self":
				send "&aYour level: %{rpgdb::level::%player's uuid%}%" to player
				send "&aYour xp: %{rpgdb::xp::%player's uuid%}% / %get_max_xp({rpgdb::level::%player's uuid%})%" to player
				#send get_level_progress_bar(player, "|", 50) to player
			else:
				send {@nopermission_message} to player
			stop
		if arg-1 is "reset":
			if player has permission "rpg.xp.modify":
				if arg-2 is not set:
					set {_target} to player
				else:
					set {_target} to arg-2 parsed as player
				set {rpgdb::xp::%{_target}'s uuid%} to 0
				set {rpgdb::level::%{_target}'s uuid%} to 1
				sync_mc_level(player)
				send "&aReset experience of %{_target}%." to player
			else:
				send {@nopermission_message} to player
			stop
		if arg-1 is "add":
			if player has permission "rpg.xp.modify":
				if arg-3 is not set:
					set {_target} to player
					set {_amount} to arg-2 parsed as number
				else:
					set {_target} to arg-2 parsed as player
					set {_amount} to arg-3 parsed as number
				add_xp({_target}, {_amount})
				send "&aAdded %{_amount}% experience to %{_target}%." to player
			else:
				send {@nopermission_message} to player
			stop
		if arg-1 is "remove":
			if player has permission "rpg.xp.modify":
				if arg-3 is not set:
					set {_target} to player
					set {_amount} to (arg-2 parsed as number)
				else:
					set {_target} to arg-2 parsed as player
					set {_amount} to (arg-3 parsed as number)
				add_xp({_target}, 0 - {_amount})
				send "&aRemoved %{_amount}% experience from %{_target}%." to player
			else:
				send {@nopermission_message} to player
			stop
		if player has permission "rpg.xp.view.others":
			set {_player} to arg-1 parsed as a player
			if {rpgdb::xp::%{_player}'s uuid%} is not set:
				send "&cUnknown player %arg-1%" to player
				stop
			send "&a%{_player}%'s level: %{rpgdb::level::%{_player}'s uuid%}%" to player
			send "&a%{_player}%'s xp: %{rpgdb::xp::%{_player}'s uuid%}% / %get_max_xp({rpgdb::level::%{_player}'s uuid%})%" to player
			#send get_level_progress_bar({_player}, "|", 50) to player
		else:
			send {@nopermission_message} to player